
: APP LDH STRB LDH 1 ADD STRH ;


: LIT
	LIT LDH STRB
	LDH 1 ADD STRH
	LDH STRC
	LDH CSZ ADD STRH
;

: NOP NOP APP ;


: JMP
	LDH 1 ADD
	666 LIT
	JMP APP
;

: JZ
	LDH 1 ADD
	666 LIT
	JZ APP
;


: CALL CALL APP ;


: RET RET APP ;



: IMMEDIATE LDL CSZ ADD DUP LDB 1 6 SHL OR SWAP STRB ;
: VISIBLE LDL CSZ ADD DUP LDB 1 7 SHL OR SWAP STRB ; IMMEDIATE
: SELF
	LDL CSZ ADD LDB 31 AND 
	LDL CSZ ADD 1 ADD ADD
	JMP STRC
; IMMEDIATE


: IF JZ ; IMMEDIATE
: THEN LDH SWAP STRC ; IMMEDIATE
: ELSE JMP SWAP LDH SWAP STRC ; IMMEDIATE


: ( KEY 41 NEQ IF SELF THEN ; IMMEDIATE
: \ KEY 10 NEQ IF SELF THEN ; IMMEDIATE
: WARN KEY
	DUP 92 EQ IF
		KEY DUP 10 EQ IF
			32 EMIT DROP DROP
		ELSE
			SWAP EMIT EMIT
		THEN
		SELF
	THEN
	DUP EMIT 10 NEQ IF SELF THEN
; IMMEDIATE
(
WARN Now warn can be made multiline by appending \ at the end of\
a line. The only caveat is that the last character before \ cant\
be \ itself \ \
Just like in the example above a, if you want the last  character\
be a \ just put a space after the backslash
)

: EXIT RET ; IMMEDIATE
: BYE HALT ;


: ALLOT LDH ADD STRH ;


: BEGIN LDH ; IMMEDIATE
: AGAIN JMP STRC ; IMMEDIATE
: UNTIL JZ STRC ; IMMEDIATE
: WHILE JZ ; IMMEDIATE
: REPEAT SWAP JMP STRC LDH SWAP STRC ; IMMEDIATE























